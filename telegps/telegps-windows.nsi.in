!addplugindir ../altosui/Instdrv/NSIS/Plugins
!addincludedir ../altosui/Instdrv/NSIS/Includes
!include x64.nsh
!include java.nsh

Name "TeleGPS Installer"

; Default install directory
InstallDir "$PROGRAMFILES\AltusMetrum"

; Tell the installer where to re-install a new version
InstallDirRegKey HKLM "Software\AltusMetrum" "Install_Dir"

LicenseText "GNU General Public License Version 2"
LicenseData "../COPYING"

; Need admin privs for Vista or Win7
RequestExecutionLevel admin

ShowInstDetails Show

ComponentText "TeleGPS Software Installer"

Function .onInit
	DetailPrint "Checking host operating system"
	${If} ${RunningX64}
		DetailPrint "Installer running on 64-bit host"
		SetRegView 64
		StrCpy $INSTDIR "$PROGRAMFILES64\AltusMetrum"
		${DisableX64FSRedirection}
	${EndIf}
FunctionEnd

; Pages to present

Page license
Page components
Page directory
Page instfiles

UninstPage uninstConfirm
UninstPage instfiles

; And the stuff to install

Section "Install Driver" InstDriver

	InstDrv::InitDriverSetup /NOUNLOAD {4D36E96D-E325-11CE-BFC1-08002BE10318} AltusMetrumSerial
	Pop $0
	DetailPrint "InitDriverSetup: $0"
	InstDrv::DeleteOemInfFiles /NOUNLOAD
	InstDrv::CreateDevice /NOUNLOAD

	SetOutPath $INSTDIR
	File "../altusmetrum.inf"
	File "../altusmetrum.cat"

	${DisableX64FSRedirection}
	IfFileExists $WINDIR\System32\PnPutil.exe 0 nopnp
		${DisableX64FSRedirection}
		nsExec::ExecToLog '"$WINDIR\System32\PnPutil.exe" -i -a "$INSTDIR\altusmetrum.inf"'
		Goto done
nopnp:
		InstDrv::InstallDriver /NOUNLOAD "$INSTDIR\altusmetrum.inf"
done:

SectionEnd

Section "TeleGPS Application"
	Call DetectJRE

	SetOutPath $INSTDIR

	File "telegps-fat.jar"
	File "altoslib_@ALTOSLIB_VERSION@.jar"
	File "altosuilib_@ALTOSUILIB_VERSION@.jar"
	File "cmudict04.jar"
	File "cmulex.jar"
	File "cmu_time_awb.jar"
	File "cmutimelex.jar"
	File "cmu_us_kal.jar"
	File "en_us.jar"
	File "freetts.jar"
	File "jfreechart.jar"
	File "jcommon.jar"

	File "*.dll"

	File "../icon/*.ico"

	CreateShortCut "$SMPROGRAMS\TeleGPS.lnk" "$SYSDIR\javaw.exe" "-jar telegps-fat.jar" "$INSTDIR\telegps.ico"
SectionEnd

Section "TeleGPS Desktop Shortcut"
	CreateShortCut "$DESKTOP\TeleGPS.lnk" "$INSTDIR\telegps-fat.jar"  "" "$INSTDIR\telegps.ico"
SectionEnd

Section "TeleGPS, TeleDongle and TeleBT Firmware"

	SetOutPath $INSTDIR

	File "../src/telegps-v1.0/telegps-v1.0-${VERSION}.ihx"
	File "../src/teledongle-v0.2/teledongle-v0.2-${VERSION}.ihx"
	File "../src/telebt-v1.0/telebt-v1.0-${VERSION}.ihx"

SectionEnd

Section "Documentation"

	SetOutPath $INSTDIR

	File "../doc/telegps.pdf"
	File "../doc/altos.pdf"
	File "../doc/telemetry.pdf"
SectionEnd

Section "Uninstaller"

	; Deal with the uninstaller

	SetOutPath $INSTDIR

	; Write the install path to the registry
	WriteRegStr HKLM SOFTWARE\AltusMetrum "Install_Dir" "$INSTDIR"

	; Write the uninstall keys for windows
	WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\AltusMetrum" "DisplayName" "Altus Metrum"
	WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\AltusMetrum" "UninstallString" '"$INSTDIR\uninstall.exe"'
	WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\AltusMetrum" "NoModify" "1"
	WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\AltusMetrum" "NoRepair" "1"

	WriteUninstaller "uninstall.exe"
SectionEnd

Section "Uninstall"
	DeleteRegKey HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\AltusMetrum"
	DeleteRegKey HKLM "Software\AltusMetrum"

	Delete "$INSTDIR\*.*"
	RMDir "$INSTDIR"

	; Remove devices
	InstDrv::InitDriverSetup /NOUNLOAD {4D36E96D-E325-11CE-BFC1-08002BE10318} AltusMetrumSerial
	InstDrv::DeleteOemInfFiles /NOUNLOAD
	InstDrv::RemoveAllDevices

	; Remove shortcuts, if any
	Delete "$SMPROGRAMS\TeleGPS.lnk"
	Delete "$DESKTOP\TeleGPS.lnk"
	
SectionEnd
